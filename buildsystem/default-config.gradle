apply plugin: 'kotlin-android'
apply plugin: 'kotlin-kapt'

android {
    compileSdkVersion project.compile_sdk_version
    buildToolsVersion project.build_tools_version

    defaultConfig {
        minSdkVersion project.min_sdk_version
        targetSdkVersion project.target_sdk_version
        versionCode project.hasProperty('versionCode') ? project.property('versionCode') as int : 1
        versionName project.hasProperty('versionName') ? project.property('versionName') : "testRun"
        buildConfigField("int", "VERSION_CODE", "$versionCode")
        buildConfigField("String", "VERSION_NAME", "\"$versionName\"")
        vectorDrawables.useSupportLibrary = true
        testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"
    }

    compileOptions {
        coreLibraryDesugaringEnabled true
        sourceCompatibility JavaVersion.VERSION_11
        targetCompatibility JavaVersion.VERSION_11
    }

    kotlinOptions {
        jvmTarget = '11'
        freeCompilerArgs += [
                // Opt-in option for Koin annotation of KoinComponent --> https://github.com/InsertKoinIO/koin/issues/939
                // Also for any OptIn requirements on experimental or deprecated APIs
                "-opt-in=kotlin.RequiresOptIn"
        ]
    }

    packagingOptions {
        resources {
            excludes += ['DebugProbesKt.bin', 'win32-x86-64/attach_hotspot_windows.dll', 'win32-x86/attach_hotspot_windows.dll', 'META-INF/AL2.0', 'META-INF/LGPL2.1', 'META-INF/licenses/ASM']
            pickFirsts += ['META-INF/main.kotlin_module', 'META-INF/atomicfu.kotlin_module', 'META-INF/kotlinx-coroutines-core.kotlin_module']
        }
    }
    lint {
        abortOnError false
    }

    sourceSets {
        main.java.srcDirs += 'src/main/kotlin'
    }

    buildFeatures {
        viewBinding true
        buildConfig true
    }

    dependencies {
        coreLibraryDesugaring desugar_jdk

        api kotlin_stdlib
        api kotlin_reflect
        api koin
        api koin_extensions
        api core_ktx
        api timber
        api coroutines_core
        api coroutines_reactive
        api coroutines_android
        api arrow_core_data
        api security_jetpack
        api support_annotations
        api moshi
        api moshi_kotlin
        api moshi_adapters
        kapt(moshi_kotlin_codegen)
    }
}
